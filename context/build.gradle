plugins {
    id 'java'
    id 'maven-publish'
    id "com.jfrog.bintray"
}

group  project.publish_group_id
version  project.publish_version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

dependencies {
    compileOnly project(":android-mock")

    testCompile 'junit:junit:4.12'

    // https://mvnrepository.com/artifact/com.google.android/android
    compileOnly files(properties.getProperty("sdk.dir") + "/platforms/android-28/android.jar")
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
    publications {
        MyPublication(MavenPublication) {
            groupId = project.publish_group_id
            artifactId = project.publish_artifact_id
            from components.java
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'android-contexts'
        userOrg = System.getenv('BINTRAY_USER')
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/zawn/android-contexts.git'
        version {
            name = project.publish_version
        }
    }
    publications = ['MyPublication']
}
